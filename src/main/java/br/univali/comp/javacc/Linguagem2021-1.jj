options {
    STATIC = false;
    UNICODE_INPUT = true;
    DEBUG_LOOKAHEAD = true;
//    DEBUG_PARSER = true;
//    DEBUG_TOKEN_MANAGER = true;
}

PARSER_BEGIN(Sintatico)
package br.univali.comp.javacc.gen;

import br.univali.comp.recovery.*;

public class Sintatico {
    private StringBuilder tokens = new StringBuilder();
    private StringBuilder lexicalErrors = new StringBuilder("");
    public StringBuilder syntacticErrors = new StringBuilder("syntactic Errors: \n");
    private Boolean hasLexicalErrors = false;
    public int contParseError = 0;

    boolean debug_recovery = true;
    Token lastError = null;

    public void handleToken()  {
            try {
                Token t = null;
                t = getNextToken();
                if(t.kind == EOF) return;
                switch(t.kind){
                    case INTEGER_CONSTANT:{
                        tokens.append("Token: '"+t.image+"' - Type: INTEGER_CONSTANT-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        break;
                    }
                    case FLOAT_CONSTANT:{
                        tokens.append("Token: '"+t.image+"' - Type: FLOAT_CONSTANT-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        break;
                    }
                    case LITERAL_CONSTANT:{
                        tokens.append("Token: '"+t.image+"' - Type: LITERAL_CONSTANT-"+t.kind+" - Line-Column-begin: "+t.beginLine+"-"+t.beginColumn+ " ; Line-Column-end: "+t.endLine+"-"+t.endColumn+"\n");
                        break;
                    }
                    // Removido de acordo com correção prof.
//                    case ARITHMETIC:{
//                        tokens.append("Token: '"+t.image+"' - Type: ARITHMETIC-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
//                        break;
//                    }
                    case IDENTIFIER:{
                        tokens.append("Token: '"+t.image+"' - Type: IDENTIFIER-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        break;
                    }
                    // Removido de acordo com correção prof.
//                    case LOGICAL:{
//                        tokens.append("Token: '"+t.image+"' - Type: LOGICAL-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
//                        break;
//                    }
//                  Simbolos especiais
                    // Pontuação
                    case SS_DOT:
                    case SS_COMMA:
                    case SS_CURLYBRACE_OPEN:
                    case SS_CURLYBRACE_CLOSE:
                    case SS_SQUACEBRACE_OPEN:
                    case SS_SQUAREBRACE_CLOSE:
                    case SS_PARENTESES_OPEN:
                    case SS_PARENTESES_CLOSE:
                    case SS_HEADER:
                    // Simbolos aritiméticos
                    case SS_PLUS:
                    case SS_MINUS:
                    case SS_TIMES:
                    case SS_DIVIDE:
                    case SS_PERCENT:
                    case SS_EXPODENTIAL:
                    case SS_MODULO:
                    // Simbolos lógicos
                    case SS_LESS_THAN:
                    case SS_MORE_THAN:
                    case SS_AND:
                    case SS_OR:
                    case SS_NOT:
                    case SS_EQUALS:
                    case SS_NOT_EQUALS:
                    case SS_LESS_OR_EQUAL_THAN:
                    case SS_MORE_OR_EQUAL_THAN: {
                        tokens.append("Token: '"+t.image+"' - Type: SPECIAL_SYMBOL-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        break;
                    }
//                  Reserved words
                    case  R_PROGRAM  :
                    case  R_DEFINE   :
                    case  R_NOT      :
                    case  R_VARIABLE :
                    case  R_IS       :
                    case  R_VERIFY   :
                    case  R_EXECUTE  :
                    case  R_SET      :
                    case  R_TO       :
                    case  R_GET      :
                    case  R_PUT      :
                    case  R_LOOP     :
                    case  R_WHILE    :
                    case  R_TRUE     :
                    case  R_FALSE    :
                    case  R_DO       :
                    case  R_NATURAL  :
                    case  R_REAL     :
                    case  R_CHAR     :
                    case  R_BOOLEAN  :
//                    case  R_IF       :
//                    case  R_ELSE     :
                    {
                        tokens.append("Token: '"+t.image+"' - Type: RESERVED_WORDS-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        break;
                    }
//                    TO generic errors
//                    case ERROR:{
//                        tokens.append("ERROR: '"+t.image+"' - Type: Invalid Token-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
//                        break;
//                    }
                    case ERROR_INVALID_SYMBOL:{
                        tokens.append("ERROR: '"+t.image+"' - Type: ERROR_INVALID_SYMBOL-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        lexicalErrors.append("ERROR: '"+t.image+"' - Type: ERROR_INVALID_SYMBOL-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        hasLexicalErrors = true;
                        break;
                    }
                    case ERROR_UNFINISHED_LITERAL_CONSTANT:{
                        tokens.append("ERROR_UNFINISHED_LITERAL_CONSTANT: \n'"+t.image+"' \n Type: ERROR_UNFINISHED_LITERAL_CONSTANT-"+t.kind+" - Line-Column-begin: "+t.beginLine+"-"+t.beginColumn+ " ; Line-Column-end: "+t.endLine+"-"+t.endColumn+"\n");
                        lexicalErrors.append("ERROR_UNFINISHED_LITERAL_CONSTANT: \n'"+t.image+"' \n Type: ERROR_UNFINISHED_LITERAL_CONSTANT-"+t.kind+" - Line-Column-begin: "+t.beginLine+"-"+t.beginColumn+ " ; Line-Column-end: "+t.endLine+"-"+t.endColumn+"\n");
                        hasLexicalErrors = true;
                        break;
                    }
                    case ERROR_INTEGER_CONSTANT_FORMAT:{
                        tokens.append("ERROR: '"+t.image+"' - Type: ERROR_INTEGER_CONSTANT_FORMAT-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        lexicalErrors.append("ERROR: '"+t.image+"' - Type: ERROR_INTEGER_CONSTANT_FORMAT-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        hasLexicalErrors = true;
                        break;
                    }
                    case ERROR_FLOAT_CONSTANT_FORMAT:{
                        tokens.append("ERROR: '"+t.image+"' - Type: ERROR_FLOAT_CONSTANT_FORMAT-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        lexicalErrors.append("ERROR: '"+t.image+"' - Type: ERROR_FLOAT_CONSTANT_FORMAT-"+t.kind+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        hasLexicalErrors = true;
                        break;
                    }
                    default:{
                        tokens.append("<DEFAULT UNFOUND: Image:"+t.image+ " | Kind:" +t.kind +">"+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        lexicalErrors.append("<ERROR - DEFAULT UNFOUND: Image:"+t.image+ " | Kind:" +t.kind +">"+" - Line-Column: "+t.beginLine+"-"+t.beginColumn+"\n");
                        hasLexicalErrors = true;
                        break;
                    }
                }
                handleToken();
            }catch(Error error){
                hasLexicalErrors = true;
                tokens.append("Error - " + error.getMessage() + "\n");
                lexicalErrors.append("Error - " + error.getMessage() + "\n");
                System.out.println(error.toString());
                handleToken();
            }
        }

    private Sintatico readInput(String args[], String textToAnalyze) {
        Sintatico sintatico = null;
        if(args.length == 0){
            System.out.println("Reading from received text!");
            java.io.InputStream targetStream = new java.io.ByteArrayInputStream(textToAnalyze.getBytes());
            sintatico = new Sintatico(targetStream);
        }
        else if(args.length == 1){
            try{
                sintatico = new Sintatico(new java.io.FileInputStream(args[0]));
            }
            catch(java.io.FileNotFoundException e){
                System.err.println(args[0] + " was not found." );
                System.err.println(e);
            }
        }
        else{
            System.out.println("Use:\njava Sintatico < inputFile");
            System.out.println("or java Sintatico inputFile");
        }
        return sintatico;
    }

    public int getCountErrors() {
        return this.contParseError;
    }

    public String getSyntacticErrors() {
        return this.syntacticErrors.toString();
    }


    public String analyze(String args[], String textToAnalyze) throws ParseException, ParseEOFException  {
            Sintatico sintatico = this.readInput(args, textToAnalyze);

            this.handleToken();
            tokens.append("<EOF>");

            try {
                if(!hasLexicalErrors){
                    sintatico.program();
                } else {
                    return lexicalErrors.toString();
                }
            } catch (ParseException ex) {
                return ex.getMessage().toString();
            }
            if(sintatico.getCountErrors() > 0){
             return "This program has + " + sintatico.getCountErrors() + " syntactic errors \n" + sintatico.getSyntacticErrors();
            }
            return "Syntactically correct!";
        }


    public static void main(String args[])  throws ParseException, ParseEOFException {
        Sintatico parser = new Sintatico( System.in ) ;
        parser.analyze(args, "");
        System.out.println("tokens: " + parser.tokens);
    }


    static public String im(int x) {
        int k;
        String s;
        s = tokenImage[x];
        k = s.lastIndexOf("\"");

        try {
            s = s.substring(1,k);
        } catch (StringIndexOutOfBoundsException e)
            {}
        return s;
    }


    boolean eof;    // variável que indica se EOF foi alcançado
    // o método abaixo consome tokens até alcançar um que pertença ao conjunto
    // de sincronização

    public void consumeUntil(RecoverySet g, ParseException e, String met)
            throws ParseEOFException, ParseException {

            Token tok;

           if ( debug_recovery) // informação sobre a recuperação
           {
               System.out.println();
               System.out.println("*** " + met + " ***");
               System.out.println("     Syncronizing Set: " + g);
           }

           if (g == null) throw e; // se o conjunto é null, propaga a exceção

           tok = getToken(1); // pega token corrente
           while ( ! eof )  // se não chegou ao fim do arquivo
           {
                if ( g.contains(tok.kind ) ) //achou um token no conjunto
                {
                     if ( debug_recovery)
                        System.out.println("     Found syncronizing token: " + im(tok.kind));
                     break;
                }
                if (debug_recovery)
                    System.out.println("     Ignoring token: " + im(tok.kind));
                getNextToken();     // pega próximo token
                tok = getToken(1);
                if (tok.kind == EOF && ! g.contains(EOF) ) // fim da entrada?
                    eof = true;
           }
           if ( tok != lastError) {
                System.out.println("erro: "+ e.getMessage());
                syntacticErrors.append(e.getMessage());
                lastError = tok;
                contParseError++;  // incrementa número de erros
           }
           if ( eof ) throw new ParseEOFException("EOF found prematurely.");
        }

    public String getResults(){
            return tokens.toString();
    }

}
PARSER_END(Sintatico)

TOKEN_MGR_DECLS :
{
	static int commentLevel = 0 ;
}




//Skip the following elements
SKIP : {
    <SPACE : " "> |
    <EOL: ("\n" | "\r" | "\n\r")> |
    <SKIP_MARKERS: "\t" | "\r" | "\r\n" | <SPACE> | <EOL>>
}

SKIP : {
    ":*" : COMMENT                     |
    <COMMENT_LINE: ":|" (~["\n", "\r"])* <EOL> >
 }
<COMMENT> SKIP : {
    "*:" : DEFAULT
}
<COMMENT> MORE : {
    <~[]>
}

TOKEN : {
    <INTEGER_CONSTANT: ["0"-"9"] (["0"-"9"])? (["0"-"9"])? >
}
TOKEN : {
    <FLOAT_CONSTANT: ["0"-"9"] (["0"-"9"])? (["0"-"9"])? (["0"-"9"])? (["0"-"9"])? "." ["0"-"9"] (["0"-"9"])? >
}

TOKEN : {
    <LITERAL_CONSTANT: ("\"" (~["\""])* "\"") | ("'" (~["'"])* "'") >
}

// Removido de acordo com correção prof.
//TOKEN: {
//    <ARITHMETIC: ["+", "-", "*", "/", "%"] | "**" | "%%" >
//}
TOKEN [IGNORE_CASE]: {
     <R_PROGRAM: "program">
  |  <R_DEFINE: "define">
  |  <R_NOT: "not">
  |  <R_VARIABLE: "variable">
  |  <R_IS: "is">
  |  <R_VERIFY: "verify">
  |  <R_EXECUTE: "execute">
  |  <R_SET: "set">
  |  <R_TO: "to">
  |  <R_GET: "get">
  |  <R_PUT: "put">
  |  <R_LOOP: "loop">
  |  <R_WHILE: "while">
  |  <R_TRUE: "true">
  |  <R_FALSE: "false">
  |  <R_DO: "do">
  |  <R_NATURAL: "natural">
  |  <R_REAL: "real">
  |  <R_CHAR: "char">
  |  <R_BOOLEAN: "boolean">
//  |  <R_IF: "if">
//  |  <R_ELSE: "else">
}
TOKEN : {
    <IDENTIFIER: ( ( ( (["a" - "z", "A" - "Z"] ("_")?) | ("_" (["a" - "z", "A" - "Z"])?) ) ["0"-"9"] ) | ("_" ["a" - "z", "A" - "Z"]) | (["a" - "z", "A" - "Z"]) )* ( ("_") | (["a" - "z", "A" - "Z"]) | (["a" - "z", "A" - "Z"] "_") | ("_" ["a" - "z", "A" - "Z"]) )>
}

// Removido de acordo com correção prof.
//TOKEN : {
//    <LOGICAL: ["<", ">", "&", "|", "!"] | "==" | "!=" | "<=" | ">=">


//TOKEN : {
//    <SPECIAL_SYMBOL: [".", ",", "{", "}", "[", "]", "(", ")"] | ":-" |
//                     // Simbolos aritimético
//                     ["+", "-", "*", "/", "%"] | "**" | "%%" |
//                     // Simbolos lógicos
//                     ["<", ">", "&", "|", "!"] | "==" | "!=" | "<=" | ">="
//    >
//}

//Special symbols
TOKEN : {
    // Pontuação
  <SS_DOT: ".">
  | <SS_COMMA: ",">
  | <SS_CURLYBRACE_OPEN: "{">
  | <SS_CURLYBRACE_CLOSE: "}">
  | <SS_SQUACEBRACE_OPEN: "[">
  | <SS_SQUAREBRACE_CLOSE: "]">
  | <SS_PARENTESES_OPEN: "(">
  | <SS_PARENTESES_CLOSE: ")">
  | <SS_HEADER:  ":-" >
    // Simbolos aritiméticos
  | <SS_PLUS: "+">
  | <SS_MINUS: "-">
  | <SS_TIMES: "*">
  | <SS_DIVIDE: "/">
  | <SS_PERCENT: "%">
  | <SS_EXPODENTIAL: "**">
  | <SS_MODULO: "%%">
  // Simbolos lógicos
  | <SS_LESS_THAN: "<">
  | <SS_MORE_THAN: ">">
  | <SS_AND: "&">
  | <SS_OR: "|">
  | <SS_NOT: "!">
  | <SS_EQUALS: "==">
  | <SS_NOT_EQUALS: "!=">
  | <SS_LESS_OR_EQUAL_THAN: "<=">
  | <SS_MORE_OR_EQUAL_THAN: ">=">
}

TOKEN : {
    <ERROR_INVALID_SYMBOL: ["\u0000" - "\u00ff"] >
    |   <ERROR_UNFINISHED_LITERAL_CONSTANT: ("\"" (~["\""])*) | "'" (~["'"])* >
    |   <ERROR_INTEGER_CONSTANT_FORMAT: ["0"-"9"] ["0"-"9"] ["0"-"9"] (["0"-"9"])+ >
    |   <ERROR_FLOAT_CONSTANT_FORMAT: (["0"-"9"] ["0"-"9"] ["0"-"9"] ["0"-"9"] ["0"-"9"] (["0"-"9"])+ "." (["0"-"9"])*) | ((["0"-"9"])+ "." ["0"-"9"] ["0"-"9"] (["0"-"9"])+) >
}

void program() throws ParseEOFException :
{
 RecoverySet g = First.program;
}
{
try{
    linguagem2021_1(g) <EOF>
}catch (ParseException e){
    consumeUntil(g, e, "program");
}
}
void linguagem2021_1(RecoverySet g) throws ParseEOFException: {}
{
    header(g) <R_PROGRAM> <SS_CURLYBRACE_OPEN> define(g) execute(g) <SS_CURLYBRACE_CLOSE> ident_final(g)
}
void header(RecoverySet g) : {}
{
    [ <SS_HEADER> <LITERAL_CONSTANT> ]
}
void ident_final(RecoverySet g) : {}
{
    [ <IDENTIFIER> ]
}
void todos_os_comandos(RecoverySet g) : {}
{
        loops_repeticao(g)
    |   verify(g)
    |   get(g)
    |   set(g)
    |   put(g)
}
void lista_de_comandos(RecoverySet g) : {}
{
    todos_os_comandos(g) <SS_DOT> lista_de_comandos_opt(g)
}
void lista_de_comandos_opt(RecoverySet g) : {}
{
    (todos_os_comandos(g) <SS_DOT>)*
}
void lista_de_identificadores(RecoverySet g) : {}
{
    <IDENTIFIER> lista_opt_de_identificadores(g)
}
void lista_opt_de_identificadores(RecoverySet g) : {}
{
    [ <SS_COMMA> lista_de_identificadores(g) ]
}
// BEGIN define
// TODO: see notes on the BNF google doc, this is a temporary implentation
// <valor> seems equal to <constante>
void define(RecoverySet g) throws ParseEOFException : {
    RecoverySet f = First.define.union(g);
 }
{
    try {
        [ <R_DEFINE> <SS_CURLYBRACE_OPEN> def_const_e_ou_var(g) <SS_CURLYBRACE_CLOSE> ]
    } catch(ParseException e) {
        consumeUntil(f, e, "execute");
    }
}
void def_const_e_ou_var(RecoverySet g) : {}
{
    define_const_var_opt(g) | define_var_opt_const(g)
}
void define_const_var_opt(RecoverySet g) : {}
{
    <R_NOT> <R_VARIABLE> lista_de_atribuicao_com_valor(g) define_var_opt(g)
}
void define_var_opt(RecoverySet g) : {}
{
    [<R_VARIABLE> lista_de_atribuicao_sem_valor(g)]
}
void lista_de_atribuicao_com_valor(RecoverySet g) : {}
{
    tipo(g) <R_IS> lista_de_identificadores(g) constante(g) <SS_DOT> [ lista_de_atribuicao_com_valor(g) ]
}

void lista_de_atribuicao_sem_valor(RecoverySet g) : {}
{
    tipo(g) <R_IS> lista_de_identificadores(g) <SS_DOT> [ lista_de_atribuicao_sem_valor(g) ]
}
void define_var_opt_const(RecoverySet g) : {}
{
    <R_VARIABLE> lista_de_atribuicao_sem_valor(g) define_const_opt(g)
}
void define_const_opt(RecoverySet g) : {}
{
    [<R_NOT> <R_VARIABLE> lista_de_atribuicao_com_valor(g) define_var_opt(g)]
}
void tipo(RecoverySet g) : {}
{
    <R_NATURAL> | <R_REAL> |  <R_CHAR> | <R_BOOLEAN>
}
// BEGIN loops
void loops_repeticao(RecoverySet g) : {}
{
    while_expr(g) | do_loop(g)
}
void while_expr(RecoverySet g) : {}
{
    <R_WHILE> expressao(g) <R_IS> <R_TRUE> <R_DO> <SS_CURLYBRACE_OPEN> lista_de_comandos(g) <SS_CURLYBRACE_CLOSE>
}
void do_loop(RecoverySet g) : {}
{
    <R_LOOP> <SS_CURLYBRACE_OPEN> lista_de_comandos(g) <SS_CURLYBRACE_CLOSE> <R_WHILE> expressao(g) <R_IS> <R_TRUE>
}
// BEGIN execute
void execute(RecoverySet g) : {}
{
    <R_EXECUTE> <SS_CURLYBRACE_OPEN> lista_de_comandos(g) <SS_CURLYBRACE_CLOSE>
}
// BEGIN get
void get(RecoverySet g) : {}
{
    <R_GET>  <SS_CURLYBRACE_OPEN> lista_de_identificadores(g) <SS_CURLYBRACE_CLOSE>
}
// BEGIN set
void set(RecoverySet g) : {}
{
    <R_SET> expressao(g) <R_TO> lista_de_identificadores(g)
}
// BEGIN put
void put(RecoverySet g) : {}
// TODO: validate this, see note on the BNF google doc
{
    <R_PUT> <SS_CURLYBRACE_OPEN> lista_de_identificadores_e_ou_constantes(g) <SS_CURLYBRACE_CLOSE>
}
void identificador_ou_constante(RecoverySet g) : {}
{
// TODO: double check this, see note on the BNF google doc
    <IDENTIFIER> | constante(g)
}
void lista_de_identificadores_e_ou_constantes(RecoverySet g) : {}
{
    identificador_ou_constante(g) [ <SS_COMMA> lista_de_identificadores_e_ou_constantes(g) ]
}
void constante(RecoverySet g) : {}
{
    <FLOAT_CONSTANT> | <INTEGER_CONSTANT> | <LITERAL_CONSTANT>
}
// BEGIN verify
void verify(RecoverySet g) : {}
{
    <R_VERIFY> expressao(g) <R_IS> verify_truefalse(g)
}
void verify_truefalse(RecoverySet g) : {}
{
    verify_true(g) | verify_false(g)
}
void verify_true(RecoverySet g) : {}
{
    <R_TRUE> <SS_CURLYBRACE_OPEN> lista_de_comandos(g) <SS_CURLYBRACE_CLOSE> verify_opt_false(g)
}
void verify_opt_false(RecoverySet g) : {}
{
  [
    <R_IS> <R_FALSE> <SS_CURLYBRACE_OPEN> lista_de_comandos(g) <SS_CURLYBRACE_CLOSE>
  ]
}
void verify_false(RecoverySet g) : {}
{
    <R_FALSE> <SS_CURLYBRACE_OPEN> lista_de_comandos(g) <SS_CURLYBRACE_CLOSE> verify_opt_true(g)
}
void verify_opt_true(RecoverySet g) : {}
{
  [
    <R_IS> <R_TRUE> <SS_CURLYBRACE_OPEN> lista_de_comandos(g) <SS_CURLYBRACE_CLOSE>
  ]
}
// BEGIN espressao
void expressao(RecoverySet g) : {}
{
    expressao_aritmetica_ou_logica(g) expressao_dash(g)
}
void expressao_dash(RecoverySet g) : {}
{
    [
          <SS_EQUALS>     expressao_aritmetica_ou_logica(g)
        | <SS_NOT_EQUALS> expressao_aritmetica_ou_logica(g)
        | <SS_LESS_THAN>  expressao_aritmetica_ou_logica(g)
        | <SS_MORE_THAN>  expressao_aritmetica_ou_logica(g)
        | <SS_LESS_OR_EQUAL_THAN> expressao_aritmetica_ou_logica(g)
        | <SS_MORE_OR_EQUAL_THAN> expressao_aritmetica_ou_logica(g)
    ]
}
void expressao_aritmetica_ou_logica(RecoverySet g) : {}
{
    termo2(g) menor_prioridade(g)
}
void menor_prioridade(RecoverySet g) : {}
{
    [
         <SS_PLUS> termo2(g) menor_prioridade(g)
       | <SS_MINUS> termo2(g) menor_prioridade(g)
       | <SS_OR> termo2(g) menor_prioridade(g)
    ]
}
void termo2(RecoverySet g) : {}
{
    termo1(g) media_prioridade(g)
}
void media_prioridade(RecoverySet g) : {}
{
    [
        <SS_TIMES>   termo1(g) media_prioridade(g)
      | <SS_DIVIDE>  termo1(g) media_prioridade(g)
      | <SS_PERCENT> termo1(g) media_prioridade(g)
      | <SS_MODULO>  termo1(g) media_prioridade(g)
      | <SS_AND>     termo1(g) media_prioridade(g)
    ]
}
void termo1(RecoverySet g) : {}
{
    elemento(g) maior_prioridade(g)
}
void maior_prioridade(RecoverySet g) : {}
{
    [
        <SS_EXPODENTIAL> elemento(g) maior_prioridade(g)
    ]
}
void elemento(RecoverySet g) : {}
{
        <IDENTIFIER> indice(g)
     |  <INTEGER_CONSTANT>
     |  <FLOAT_CONSTANT>
     |  <LITERAL_CONSTANT>
     |  <R_TRUE>
     |  <R_FALSE>
     |  <SS_PARENTESES_OPEN> expressao(g) <SS_PARENTESES_CLOSE>
     |  <SS_NOT> <SS_PARENTESES_OPEN> expressao(g) <SS_PARENTESES_CLOSE>
}
void indice(RecoverySet g) : {}
{
    [ <SS_SQUACEBRACE_OPEN> <INTEGER_CONSTANT> <SS_PARENTESES_CLOSE> ]
}